defaultTasks 'clean', 'screamCompile'

allprojects {
    group = 'org.screamingsandals.bedwars'
    version = '0.3.0-SNAPSHOT'
}

buildscript {
    repositories {
        maven { url 'https://repo.screamingsandals.org/public/' }
    }
    dependencies {
		classpath 'org.screamingsandals.gradle.builder:screaming-plugin-builder:1.0.31'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.screamingsandals.gradle.builder'

    repositories {
        maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/'  }
        maven { url 'https://repo.onarandombox.com/content/groups/public'  }
        maven { url 'https://ci.frostcast.net/plugin/repository/everything'  }
        maven { url 'https://repo.codemc.org/repository/maven-public/'  }
        maven { url 'https://jitpack.io' }
        maven { url 'https://repo.citizensnpcs.co'  }
        /* Our gitlab repo for scoreboard lib*/
        maven { url 'https://gitlab.hoznet.dev/api/v4/projects/13/packages/maven' }
    }
    
    dependencies {
    	compileOnly paper("1.16.4")
    }

    shadowJar {
        relocate 'com.zaxxer', 'org.screamingsandals.bedwars.lib.HikariCP'
        relocate 'org.screamingsandals.simpleinventories', 'org.screamingsandals.bedwars.lib.sgui'
        relocate 'org.bstats', 'org.screamingsandals.bedwars.lib.bstats'
        relocate 'io.papermc.lib', 'org.screamingsandals.lib.paperlib'
        relocate 'pronze.lib', 'org.screamingsandals.lib.pronze'

        if (System.getenv('BUILD_NUMBER') != null) {
            archiveClassifier.set(System.getenv('BUILD_NUMBER'))
        } else {
            archiveClassifier.set('dev')
        }
    }

    sourceCompatibility = '11.0'

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }
}
